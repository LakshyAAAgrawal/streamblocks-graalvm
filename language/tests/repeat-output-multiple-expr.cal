namespace simple.dwf:
    /*
     * Source actor 
     * Produces 16 token from 0 to 15, and send them to output OUT.
     */
    actor Source()
        ==> int OUT:

        int Counter := 0;
        List l1 := [0, 1, 2, 3];
        List l2 := [4, 5, 6, 7];

        action ==> OUT:[token1, token2] repeat 4
        guard
            Counter < 4
        var
            List token1 = l1,
            List token2 = l2
        do
            Counter := Counter + 1;
            l1 := [ 4 + i: for int i in token1 ];
            l2 := [ 4 + i: for int i in token2 ];
        end

    end

    /*
     * Sink actor
     * Consumes identfinetly tokens from the input port IN, 
     * and prints the tokens 4 by 4
     */
    actor Sink()
        int IN ==> :
    
        action IN:[token] repeat 8 ==>
        do 
            println("print starts");
            foreach int i in token do
                println(i);
            end
        end
    end

    /*
     * SourceSink network
     * Instatiates Source and Sink actors and interconnects them.
     */
    network SourceSink()
        ==> :

        entities
            source = Source();
            sink = Sink();
        structure
            source.OUT --> sink.IN;
    end

end

